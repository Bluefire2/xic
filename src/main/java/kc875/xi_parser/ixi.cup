package kc875.xi_parser;
// A stub for a Xi CUP specification.

import kc875.ast.*;
import kc875.lexer.XiLexer;
import kc875.lexer.XiToken;
import polyglot.util.Pair;

import java.util.List;
import java.util.ArrayList;
import java.util.stream.Collectors;
import java.util.Collections;

import kc875.lexer.XiTokenFactory;
import java_cup.runtime.Symbol;
import kc875.xic_error.LexicalError;
import kc875.xic_error.SyntaxError;

parser code
{:
    XiLexer lexer;

    public IxiParser (XiLexer l, XiTokenFactory xtf) {
        super(l,xtf);
        this.lexer = l;
    }

    public void syntax_error(Symbol s) {
        XiToken t = (XiToken) s;
        String message = String.format(
            "Unexpected token %s", t.getValue()
        );
        throw new SyntaxError(message, t.getLocation());
    }
:};

scan with
{:
    Symbol s = lexer.next_token(); // s is an XiToken wrapped as a Symbol
    XiToken t = (XiToken) s;
    if (t.sym == sym.ERROR) {
        throw new LexicalError(t.getValue().toString(), t.getLocation());
    } else {
        return s;
    }
:};

terminal String STRING_LIT;
terminal Character CHAR_LIT;
terminal Long INT_LIT;
terminal Boolean BOOL_LIT;
terminal INT_TYPE, BOOL_TYPE;

terminal
    UNDERSCORE,
    USE,
    IF,
    WHILE,
    ELSE,
    RETURN,
    LENGTH,

    NULL,
    THIS,
    NEW,
    EXTENDS,
    CLASS,
    DOT,// never used though; need it so that Lexer can produce it
    BREAK,

    EQ,
    MINUS,
    PLUS,
    NOT,
    MULT,
    HI_MULT,
    DIV,
    MOD,
    EQEQ,
    NEQ,
    GT,
    LT,
    GTEQ,
    LTEQ,
    AND,
    OR,
    UMINUS, //maybe not necessary

    COLON,
    SEMICOLON,
    COMMA,
    LPAREN,
    RPAREN,
    LBRAC,
    RBRAC,
    LCURL,
    RCURL,
    ERROR;
terminal String ID;

nonterminal FileInterface interface;
nonterminal interface_file;
nonterminal List<String> id_list;
nonterminal Expr expr, assignable_indexable_expr;
nonterminal List<Expr> expr_list, indices;
nonterminal UseInterface use_interface;
nonterminal List<UseInterface> import_list;

nonterminal Pair<String, TypeTTau> annotated_var, decl_annotated_var;
nonterminal TypeTTau type, basic_type, init_list_type, list_type;
nonterminal FuncDecl func_decl;

nonterminal List<TypeTTau> type_list;
nonterminal List<Pair<String, TypeTTau>> annotated_var_list, func_args;

//pa7 new stuff
nonterminal ClassDecl class_decl;
nonterminal StmtDecl field;
nonterminal List<StmtDecl> field_list;
nonterminal StmtDeclMulti multi_var_decl;
nonterminal List<TopLevelDecl> decl_or_defn_list;
nonterminal List<FuncDecl> func_decl_list;

precedence left COMMA;
precedence left OR;
precedence left AND;
precedence left NEQ, EQEQ;
precedence left LT, GT, LTEQ, GTEQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV, MOD, HI_MULT;
precedence nonassoc UMINUS, NOT;
precedence left LBRAC, RBRAC, LPAREN, RPAREN;

start with interface_file;

id_list ::=
  ID:i1 COMMA ID:i2 {:
    ArrayList<String> l = new ArrayList<>();
    l.add(i1);
    l.add(i2);
    RESULT = l; :}
| id_list:l COMMA ID:i {:
    l.add(i);
    RESULT = l;
  :}
;
// TYPES

basic_type ::=
  BOOL_TYPE:t {: RESULT = new TypeTTauBool(); :}
| INT_TYPE:t {: RESULT = new TypeTTauInt(); :}
| ID:i {: RESULT = new TypeTTauClass(i); :}
;

// Types legal for use everywhere
// Does not allow list types with initialized length
// No need to unwrap
type ::=
  basic_type:b {: RESULT = b; :}
| list_type:t {: RESULT = t; :}
;

list_type ::=
  basic_type:t LBRAC RBRAC {: RESULT = new TypeTTauArray(t); :}
| list_type:t LBRAC RBRAC {: RESULT = new TypeTTauArray(t); :}
;

indices ::=
  LBRAC expr:e RBRAC {: ArrayList<Expr> l = new ArrayList<>(); l.add(e); RESULT = l; :}
| indices:l LBRAC expr:e RBRAC {: l.add(e); RESULT = l; :}
;

// List types with initialized lengths, can only be used in decl w/o assign
// Example: int[3][n]
init_list_type ::=
  basic_type:t indices:i {:
    TypeTTau curr = t;
    List<Expr> lengths = i;
    Collections.reverse(lengths);
    for (Expr e : lengths){
        curr = new TypeTTauArray(curr, e);
    }
    RESULT = curr;
  :}
| basic_type:t indices:i LBRAC RBRAC {:
    TypeTTau curr = new TypeTTauArray(t);
    List<Expr> lengths = i;
    Collections.reverse(lengths);
    for (Expr e : lengths){
        curr = new TypeTTauArray(curr, e);
    }
    RESULT = curr;
  :}
| init_list_type:t LBRAC RBRAC {:
    TypeTTauArray curr = (TypeTTauArray) t;
    while (curr.getTypeTTau() instanceof TypeTTauArray){
        curr = (TypeTTauArray) curr.getTypeTTau();
    }
    curr.setTypeTTau(new TypeTTauArray(curr.getTypeTTau()));
    RESULT = t;
  :}
;

type_list ::=
  type:t {:
    ArrayList<TypeTTau> l = new ArrayList<>();
    l.add(t);
    RESULT = l; :}
| type_list:l COMMA type:t {: l.add(t); RESULT = l; :}
;

annotated_var ::=
  ID:i COLON type:t {: RESULT = new Pair<String, TypeTTau>(i, t); :}
;

annotated_var_list ::=
  annotated_var:a {:
    ArrayList<Pair<String, TypeTTau>> l = new ArrayList<>();
    l.add(a);
    RESULT = l; :}
| annotated_var_list:l COMMA annotated_var:a {: l.add(a); RESULT = l; :}
;

decl_annotated_var ::=
  ID:i COLON init_list_type:t {: RESULT = new Pair<String, TypeTTau>(i, t); :}
;

// TODO: add to the top
multi_var_decl ::=
  id_list:l COLON type:t {: RESULT = new StmtDeclMulti(l, t, lxleft); :}
| id_list:l COLON init_list_type:t {: RESULT = new StmtDeclMulti(l, t, lxleft); :}
;

// EXPRESSIONS

expr_list ::= // NOTE: THIS DOES NOT SUPPORT COMMAS AT THE END
  expr:e {:
       ArrayList<Expr> l = new ArrayList<>();
       l.add(e);
       RESULT = l; :}
| expr_list:l COMMA expr:e {: l.add(e); RESULT = l; :}
;

// Exprs without ( or { on LHS
assignable_indexable_expr ::=
  STRING_LIT:s {: RESULT = new ExprArrayLiteral(s, sxleft); :} // strings are really lists of ints
| ID:i {: RESULT = new ExprId(i, ixleft); :}
;

expr ::=
  assignable_indexable_expr:e {: RESULT = e; :}
// Array literals
| LCURL:l RCURL {: RESULT = new ExprArrayLiteral(new ArrayList<Expr>(), lxleft); :}
| LCURL:l expr_list:e RCURL {: RESULT = new ExprArrayLiteral(e, lxleft); :}
| LCURL:l expr_list:e COMMA RCURL {: RESULT = new ExprArrayLiteral(e, lxleft); :}
// Grouping expressions
| LPAREN expr:e RPAREN {: RESULT = e; :}
// Binops
| expr:e1 AND:o expr:e2 {: RESULT = new ExprBinop(Binop.AND, e1, e2, oxleft); :}
| expr:e1 OR:o expr:e2 {: RESULT = new ExprBinop(Binop.OR, e1, e2, oxleft); :}
| expr:e1 PLUS:o expr:e2 {: RESULT = new ExprBinop(Binop.PLUS, e1, e2, oxleft); :}
| expr:e1 MINUS:o expr:e2 {: RESULT = new ExprBinop(Binop.MINUS, e1, e2, oxleft); :}
| expr:e1 DIV:o expr:e2 {: RESULT = new ExprBinop(Binop.DIV, e1, e2, oxleft); :}
| expr:e1 MOD:o expr:e2 {: RESULT = new ExprBinop(Binop.MOD, e1, e2, oxleft); :}
| expr:e1 MULT:o expr:e2 {: RESULT = new ExprBinop(Binop.MULT, e1, e2, oxleft); :}
| expr:e1 HI_MULT:o expr:e2 {: RESULT = new ExprBinop(Binop.HI_MULT, e1, e2, oxleft); :}
| expr:e1 EQEQ:o expr:e2 {: RESULT = new ExprBinop(Binop.EQEQ, e1, e2, oxleft); :}
| expr:e1 NEQ:o expr:e2 {: RESULT = new ExprBinop(Binop.NEQ, e1, e2, oxleft); :}
| expr:e1 GT:o expr:e2 {: RESULT = new ExprBinop(Binop.GT, e1, e2, oxleft); :}
| expr:e1 LT:o expr:e2 {: RESULT = new ExprBinop(Binop.LT, e1, e2, oxleft); :}
| expr:e1 GTEQ:o expr:e2 {: RESULT = new ExprBinop(Binop.GTEQ, e1, e2, oxleft); :}
| expr:e1 LTEQ:o expr:e2 {: RESULT = new ExprBinop(Binop.LTEQ, e1, e2, oxleft); :}
// Unops
| MINUS:o expr:e {: RESULT = new ExprUnop(Unop.UMINUS, e, oxleft); :} %prec UMINUS
| NOT:o expr:e {: RESULT = new ExprUnop(Unop.NOT, e, oxleft); :}
// Literals
| BOOL_LIT:b {: RESULT = new ExprBoolLiteral(b, bxleft); :}
| INT_LIT:i {: RESULT = new ExprIntLiteral(i, ixleft); :}
| CHAR_LIT:c {: RESULT = new ExprIntLiteral(c, cxleft); :} // chars are really ints
| expr:e1 indices:i {:
    Expr curr = e1;
    for (Expr e : i) {
        curr = new ExprIndex(curr, e, ixleft);
    }
    RESULT = curr; :}
;

// FUNCTION DECLARATIONS

func_args ::=
  LPAREN RPAREN {: RESULT = new ArrayList<Pair<String, TypeTTau>>(); :}
| LPAREN annotated_var_list:a RPAREN {: RESULT = a; :}
;

func_decl ::=
  ID:n func_args:p {: RESULT = new FuncDecl(n, p, nxleft); :}
| ID:n func_args:p COLON type_list:r {:
    // r.size() >= 1
    TypeT rt;
    if (r.size() == 1) {
        rt = r.get(0);
    } else {
        rt = new TypeTList(r);
    }
    RESULT = new FuncDecl(n, p, rt, nxleft); :}
;


//CLASSES

func_decl_list ::=
  func_decl:d {:
      ArrayList<FuncDecl> l = new ArrayList<>();
      l.add(d);
      RESULT = l; :}
| func_decl_list:l func_decl:d {: l.add(d); RESULT = l; :}
;

field ::=
  decl_annotated_var:d {:
    RESULT = new StmtDeclSingle(new TypeDeclVar(d), dxleft); :}
| annotated_var:d {:
    RESULT = new StmtDeclSingle(new TypeDeclVar(d), dxleft); :}
| multi_var_decl:m {:
    RESULT = m; :}
| field:f SEMICOLON {: RESULT = f; :}
;

field_list ::=
  field:f {:
    ArrayList<StmtDecl> l = new ArrayList<>();
    l.add(f);
    RESULT = l; :}
| field_list:l field:f {:
    l.add(f);
    RESULT = l; :}
;
//  decl_annotated_var:d {:
//    ArrayList<StmtDecl> l = new ArrayList<>();
//    l.add(new StmtDeclSingle(new TypeDeclVar(d), dxleft));
//    RESULT = l; :}
//| annotated_var:d {:
//    ArrayList<StmtDecl> l = new ArrayList<>();
//    l.add(new StmtDeclSingle(new TypeDeclVar(d), dxleft));
//    RESULT = l; :}
//| multi_var_decl:m {:
//    ArrayList<StmtDecl> l = new ArrayList<>();
//    l.add(m);
//    RESULT = l; :}
//| field_list:l multi_var_decl:m {:
//    l.add(m);
//    RESULT = l; :}
//| field_list:l annotated_var:d {:
//    l.add(new StmtDeclSingle(new TypeDeclVar(d), dxleft)); RESULT = l; :}
//| field_list:l decl_annotated_var:d {:
//    l.add(new StmtDeclSingle(new TypeDeclVar(d), dxleft)); RESULT = l; :}
//;

class_decl ::=
  CLASS ID:c LCURL field_list:fl func_decl_list:dl RCURL {:
    RESULT = new ClassDecl(c, fl, dl, cxleft); :}
| CLASS ID:c LCURL func_decl_list:dl RCURL {:
    RESULT = new ClassDecl(c, new ArrayList<>(), dl, cxleft); :}
| CLASS ID:c LCURL field_list:fl RCURL {:
      RESULT = new ClassDecl(c, fl, new ArrayList<>(), cxleft); :}
| CLASS ID:c LCURL RCURL {:
      RESULT = new ClassDecl(c, new ArrayList<>(), new ArrayList<>(), cxleft); :}
| CLASS ID:c EXTENDS ID:e LCURL field_list:fl func_decl_list:dl RCURL {:
    RESULT = new ClassDecl(c, e, fl, dl, cxleft); :}
| CLASS ID:c EXTENDS ID:e LCURL func_decl_list:dl RCURL {:
    RESULT = new ClassDecl(c, e, new ArrayList<StmtDecl>(), dl, cxleft); :}
| CLASS ID:c EXTENDS ID:e LCURL field_list:fl RCURL {:
      RESULT = new ClassDecl(c, e, fl, new ArrayList<FuncDecl>(), cxleft); :}
| CLASS ID:c EXTENDS ID:e LCURL RCURL {:
      RESULT = new ClassDecl(c, e, new ArrayList<StmtDecl>(), new ArrayList<FuncDecl>(), cxleft); :}
;

// unordered list of function decl or class decl...
//different from nonterminal of same name in xiparser b/c we don't allow defns and global
decl_or_defn_list ::=
  class_decl:c {:
    ArrayList<TopLevelDecl> l = new ArrayList<>();
    l.add(c);
    RESULT = l; :}
| func_decl:d {:
      ArrayList<TopLevelDecl> l = new ArrayList<>();
      l.add(d);
      RESULT = l; :}
| decl_or_defn_list:l func_decl:d {: l.add(d); RESULT = l; :}
| decl_or_defn_list:l class_decl:c {: l.add(c); RESULT = l; :}
;

// IMPORTS

use_interface ::=
  USE ID:i {: RESULT = new UseInterface(i, ixleft); :}
| USE ID:i SEMICOLON {: RESULT = new UseInterface(i, ixleft); :}
;

import_list ::=
  use_interface:i {:
      ArrayList<UseInterface> l = new ArrayList<>();
      l.add(i);
      RESULT = l; :}
| import_list:l use_interface:i {: l.add(i); RESULT = l; :}
;

// HIGHEST LEVEL
interface ::=
  import_list:il decl_or_defn_list:dl {:
    RESULT = new FileInterface(il, dl, ilxleft); :}
| decl_or_defn_list:dl {:
    RESULT = new FileInterface(new ArrayList<UseInterface>(), dl, dlxleft); :}
;

interface_file ::=
  {: // do nothing, empty interface file :}
| interface:i {: RESULT = i; :};
