package xi_parser;
// A stub for a Xi CUP specification.

import ast.*;
import lexer.XiLexer;
import lexer.XiToken;
import polyglot.util.Pair;

import java.util.List;
import java.util.ArrayList;
import java.util.stream.Collectors;
import java.util.Collections;

import lexer.XiTokenFactory;
import java_cup.runtime.Symbol;
import xic_error.LexicalError;
import xic_error.SyntaxError;

parser code
{:
    XiLexer lexer;

    public IxiParser (XiLexer l, XiTokenFactory xtf) {
        super(l,xtf);
        this.lexer = l;
    }

    public void syntax_error(Symbol s) {
        XiToken t = (XiToken) s;
        String message = String.format(
            "Unexpected token %s", t.getValue()
        );
        throw new SyntaxError(message, t.getLocation());
    }
:};

scan with
{:
    Symbol s = lexer.next_token(); // s is an XiToken wrapped as a Symbol
    XiToken t = (XiToken) s;
    if (t.sym == sym.ERROR) {
        throw new LexicalError(t.getValue().toString(), t.getLocation());
    } else {
        return s;
    }
:};

terminal String ID;
terminal String STRING_LIT;
terminal Character CHAR_LIT;
terminal Long INT_LIT;
terminal Boolean BOOL_LIT;
terminal INT_TYPE, BOOL_TYPE;

terminal
    UNDERSCORE,
    USE,
    IF,
    WHILE,
    ELSE,
    RETURN,
    LENGTH,

    EQ,
    MINUS,
    PLUS,
    NOT,
    MULT,
    HI_MULT,
    DIV,
    MOD,
    EQEQ,
    NEQ,
    GT,
    LT,
    GTEQ,
    LTEQ,
    AND,
    OR,
    UMINUS, //maybe not necessary

    COLON,
    SEMICOLON,
    COMMA,
    LPAREN,
    RPAREN,
    LBRAC,
    RBRAC,
    LCURL,
    RCURL,
    ERROR;

nonterminal FileInterface interface;
nonterminal interface_file;

nonterminal Pair annotated_var;
nonterminal TypeTTau type, basic_type, list_type;
nonterminal FuncDecl func_decl;

nonterminal ArrayList<TypeTTau> type_list;
nonterminal ArrayList<Pair<String, TypeTTau>> annotated_var_list, func_args;

precedence left COMMA;
precedence left LBRAC, RBRAC, LPAREN, RPAREN;

start with interface_file;

// TYPES

basic_type ::=
  BOOL_TYPE:t {: RESULT = new TypeTTauBool(); :}
| INT_TYPE:t {: RESULT = new TypeTTauInt(); :}
;

// Types legal for use everywhere
// Does not allow list types with initialized length
// No need to unwrap
type ::=
  basic_type:b {: RESULT = b; :}
| list_type:t {: RESULT = t; :}
;

list_type ::=
  basic_type:t LBRAC RBRAC {: RESULT = new TypeTTauArray(t); :}
| list_type:t LBRAC RBRAC {: RESULT = new TypeTTauArray(t); :}
;

type_list ::=
  type:t {:
    ArrayList<TypeTTau> l = new ArrayList<>();
    l.add(t);
    RESULT = l; :}
| type_list:l COMMA type:t {: l.add(t); RESULT = l; :}
;

annotated_var_list ::=
  annotated_var:a {:
    ArrayList<Pair<String, TypeTTau>> l = new ArrayList<>();
    l.add(a);
    RESULT = l; :}
| annotated_var_list:l COMMA annotated_var:a {: l.add(a); RESULT = l; :}
;

annotated_var ::=
  ID:i COLON type:t {: RESULT = new Pair<String, TypeTTau>(i, t); :}
;

// FUNCTION DECLARATIONS

func_args ::=
  LPAREN RPAREN {: RESULT = new ArrayList<Pair<String, TypeTTau>>(); :}
| LPAREN annotated_var_list:a RPAREN {: RESULT = a; :}
;

func_decl ::=
  ID:n func_args:p {: RESULT = new FuncDecl(n, p, nxleft); :}
| ID:n func_args:p COLON type_list:r {:
    // r.size() >= 1
    TypeT rt;
    if (r.size() == 1) {
        rt = r.get(0);
    } else {
        rt = new TypeTList(r);
    }
    RESULT = new FuncDecl(n, p, rt, nxleft); :}
;

// HIGHEST LEVEL

interface ::=
  func_decl:d {:
      ArrayList<FuncDecl> l = new ArrayList<>();
      l.add(d);
      RESULT = new FileInterface(l, dxleft); :}
| interface:i func_decl:d {: i.addFuncDecl(d); RESULT = i; :}
;

interface_file ::=
  {: // do nothing, empty interface file :}
| interface:i {: RESULT = i; :};
